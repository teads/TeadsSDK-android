apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion projectSdkVersion

    String sufixBaseName = ""

    if (System.getenv("BITRISE_GIT_BRANCH") != null) {
        sufixBaseName = "_" + System.getenv("BITRISE_GIT_BRANCH")
    }

    defaultConfig {
        applicationId "tv.teads.teadssdkdemo"
        minSdkVersion libraryMinSdkVersion
        targetSdkVersion libraryTargetSdkVersion
        versionCode libraryVersionCode
        versionName libraryVersion
        buildToolsVersion projectBuildToolsVersion
        archivesBaseName = "teads-sdkdemo-" + versionName + sufixBaseName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
    }
}

// Teads Repository
repositories {
    maven {
        url "http://dl.bintray.com/teads/TeadsSDK-android"
    }
    maven {
        url "https://maven.google.com"
    }
    maven {
        url "https://s3.amazonaws.com/moat-sdk-builds"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation('com.google.android.gms:play-services-ads:15.0.1') {
        exclude group: "com.android.support", module: "customtabs"
        // CustomTabs include a support library version different from the project one (older)
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.greenrobot:eventbus:3.0.0'
    // Teads SDK
    implementation("tv.teads.sdk.android:sdk:$libraryVersion@aar") {
        transitive = true
    }
    // Teads Admob Adapter
    implementation "tv.teads.sdk.android:admobadapter:$libraryVersion"
    implementation "tv.teads.sdk.android:mopubadapter:$libraryVersion"
    implementation('com.mopub:mopub-sdk:5.0.0@aar') {
        transitive = true
    }
    implementation project(':webviewhelper')

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "junit:junit:$junitVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    // THe project still use the Support Library
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
