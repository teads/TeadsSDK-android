apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt' // For ButterKnife

android {
    compileSdkVersion projectSdkVersion

    String sufixBaseName = ""

    if (System.getenv("BUILD_ID") != null) {
        sufixBaseName = "_b" + System.getenv("BUILD_ID")
    }

    defaultConfig {
        applicationId "tv.teads.teadssdkdemo"
        minSdkVersion libraryMinSdkVersion
        targetSdkVersion libraryTargetSdkVersion
        versionCode libraryVersionCode
        versionName libraryVersion
        buildToolsVersion projectBuildToolsVersion
        archivesBaseName = "teads-sdkdemo-" + versionName + sufixBaseName
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources true
        }
    }
}

// Teads Repository
repositories {
    maven {
        url "http://dl.bintray.com/teads/TeadsSDK-android"
    }
    maven {
        url "https://maven.google.com"
    }
    maven {
        url "https://s3.amazonaws.com/moat-sdk-builds"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation ('com.google.android.gms:play-services-ads:15.0.1') {
        exclude group: "com.android.support", module: "customtabs"
        // CustomTabs include a support library version different from the project one (older)
    }
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'org.greenrobot:eventbus:3.0.0'
    // Teads SDK
    implementation("tv.teads.sdk.android:sdk:$libraryVersion@aar") {
        transitive = true
    }
    // Teads Admob Adapter
    implementation "tv.teads.sdk.android:admobadapter:$libraryVersion"
    implementation "tv.teads.sdk.android:mopubadapter:$libraryVersion"
    implementation('com.mopub:mopub-sdk:5.0.0@aar') {
        transitive = true
    }
    implementation project(':webviewhelper')
}
